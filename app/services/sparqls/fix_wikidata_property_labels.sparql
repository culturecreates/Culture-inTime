PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

insert {
     ?p rdfs:label ?label
}
where {
    # federated query to get labels for wd:P-numbers
    values ?languages {
        "en" "fr" "de" 
    }
    service <https://query.wikidata.org/sparql> {
        ?uri rdfs:label ?label .
        filter(lang(?label) = ?languages )
    }
    {
        # subquery to get all Wikidata properties that need labels  
        select distinct ?uri ?p
        where {
            values ?object_type { owl:ObjectProperty owl:DatatypeProperty}
            ?p a ?object_type .
            ?sub ?p ?obj .  # Is being used
                OPTIONAL {
                  ?p  rdfs:label ?label .
                }
                filter(!bound(?label))
          
              filter(contains(str(?p),"prop/statement/P") || contains(str(?p),"prop/qualifier/P") )
              filter(!bound(?label))
            bind(URI(concat("http://www.wikidata.org/entity/P",strafter(str(?p),"/P"))) as ?uri)
            
        }
    }
}
