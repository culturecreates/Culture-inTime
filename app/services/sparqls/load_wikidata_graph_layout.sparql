PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX ex: <http://example.com/>
construct {
    ?uri a <http://wikiba.se/ontology#Item> .

    ?uri ?forward_prop2 ?statement .
    ?statement ?prop_statement2 ?obj .
    ?obj rdfs:label ?obj_label.
    #qual
    ?statement ?qualifier_prop ?qual_obj .
    ?qual_obj rdfs:label ?qual_obj_label .
    #ref
    ?statement prov:wasDerivedFrom ?reference .
    ?reference  ?reference_prop  ?reference_obj .
    ?reference_obj rdfs:label ?reference_obj_label .
}
where {
    values ?uri {
        <entity_uri_placeholder>
    }
    values ?forward_prop {
        <forward_prop_placeholder>
    }
    values ?qualifier_prop {
        <qualifier_prop_placeholder> 
    }
    values ?reference_prop {
        <reference_prop_placeholder>
    }
    #values ?reverse_prop {<http://www.wikidata.org/prop/direct/P5869> <http://www.wikidata.org/prop/direct/P4646> }
    values ?lang {
        "de"  "fr"  "en"  
    }
    {
       bind(URI(concat("http://www.wikidata.org/prop/",STRAFTER(str(?forward_prop),"/direct/")))  as ?forward_prop2)
       bind(URI(concat("http://www.wikidata.org/prop/statement/",STRAFTER(str(?forward_prop),"/direct/")))  as ?prop_statement2)
       ?uri ?forward_prop ?obj .
       ?uri ?forward_prop2 ?statement .
       ?statement ?prop_statement2 ?obj .
       filter(isURI(?obj))
        OPTIONAL {
            ?obj rdfs:label ?obj_label .
            filter(lang(?obj_label) =  ?lang)
        }
        # Qualifiers
        OPTIONAL {
            
            ?statement ?qualifier_prop ?qual_obj .
            OPTIONAL {
                ?qual_obj rdfs:label ?qual_obj_label .
                filter(lang(?qual_obj_label) =  ?lang)
            }
        }
        # References
         OPTIONAL {
            # References
            ?statement prov:wasDerivedFrom ?reference .
            ?reference  ?reference_prop  ?reference_obj .
            OPTIONAL {
                ?reference_obj rdfs:label ?reference_obj_label .
                filter(lang(?reference_obj_label) =  ?lang)
            }
        }
    }
    UNION 
    {
        bind(URI(concat("http://www.wikidata.org/prop/",STRAFTER(str(?forward_prop),"/direct/")))  as ?forward_prop2)
      bind(URI(concat("http://www.wikidata.org/prop/statement/",STRAFTER(str(?forward_prop),"/direct/")))  as ?prop_statement2)
   
      ?uri ?forward_prop ?obj .
      ?uri ?forward_prop2 ?statement .
      ?statement ?prop_statement2 ?obj .
    
        filter(isLITERAL(?obj))
        filter((lang(?obj) = ?lang) || !(datatype(?obj) = rdf:langString ) )

        # Qualifiers
        OPTIONAL {
            ?statement ?qualifier_prop ?qual_obj .
            OPTIONAL {
                ?qual_obj rdfs:label ?qual_obj_label .
                filter(lang(?qual_obj_label) =  ?lang)
            }
        }
        # References
        OPTIONAL {
            ?statement prov:wasDerivedFrom ?reference .
            ?reference  ?reference_prop  ?reference_obj .
            OPTIONAL {
                ?reference_obj rdfs:label ?reference_obj_label .
                filter(lang(?reference_obj_label) =  ?lang)
            }
        }
    }
}