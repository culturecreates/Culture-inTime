PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX prov: <http://www.w3.org/ns/prov#>

insert {
    <<?s ?wdt ?o>> ?pq ?qualifier .
    <<?s ?wdt ?o>> prov:wasDerivedFrom ?reference .
} 

where {
  ?s ?wdt ?o .
  filter(contains(str(?wdt),"/prop/direct/"))

  # switch predicate prefix from wdt: to p:
  bind(URI(concat("http://www.wikidata.org/prop/",strafter(str(?wdt),"/prop/direct/"))) as ?p )
  bind(URI(concat("http://www.wikidata.org/prop/statement/",strafter(str(?wdt),"/prop/direct/"))) as ?ps )
  ?s ?p ?statement .
  ?statement  ?ps ?o .

  # qualifiers
  OPTIONAL {
      ?statement ?pq ?qualifier .
      filter(contains(str(?pq),"/prop/qualifier/"))
  }

  # references
  OPTIONAL {
      ?statement prov:wasDerivedFrom ?reference .
  }
}