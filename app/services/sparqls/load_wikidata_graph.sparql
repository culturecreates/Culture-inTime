PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>

construct {
    <entity_uri_placeholder> ?p ?o .
    ?p rdfs:label ?prop_label .
    ?o rdfs:label ?obj_label .
    <entity_uri_placeholder> ?prop ?statement .
    ?statement ?prop_statement ?o ; prov:wasDerivedFrom ?wasDerivedFrom .
    ?wasDerivedFrom ?derived_pred ?dervied_obj .
    ?dervied_obj rdfs:label ?dervied_obj_label .
    ?statement ?qualifier_prop ?qualifier_obj .
    ?qualifier_prop rdfs:label ?qualifier_prop_label .      
    ?qualifier_obj rdfs:label ?qualifier_obj_label . 
}

where {
    # Set properties from layout
    values ?p {
        rdf:type 
        rdfs:label 
        <http://www.wikidata.org/prop/direct/P161> 
        <http://www.wikidata.org/prop/direct/P31> 
        <http://www.wikidata.org/prop/P10527> 
        <http://www.wikidata.org/prop/direct/P10527>  
        <http://www.wikidata.org/prop/direct/P937>
    }
    <entity_uri_placeholder> a <http://wikiba.se/ontology#Item> .
    <entity_uri_placeholder> ?p ?o .
   
    OPTIONAL {
        ?p rdfs:label ?prop_label .
        values ?langs  {"languages_placeholder" }
        filter(lang(?prop_label) = ?langs)
    }
    OPTIONAL {
        ?o rdfs:label ?obj_label .
        values ?langs  {"languages_placeholder" }
        filter(lang(?obj_label) = ?langs)
    }

    # Add reference triples from layout
    OPTIONAL {
        <entity_uri_placeholder> ?prop ?statement .
        ?statement ?prop_statement ?o .
        filter(contains(str(?prop),"/prop/P"))
        ?statement prov:wasDerivedFrom ?wasDerivedFrom .
        ?wasDerivedFrom ?derived_pred ?dervied_obj .
        OPTIONAL {
            ?dervied_obj rdfs:label ?dervied_obj_label .
            values ?langs  {"languages_placeholder" }
            filter(lang(?dervied_obj_label) = ?langs)
        }
    }

    # Add qualifier triples from layout
    OPTIONAL {
        <entity_uri_placeholder> ?prop ?statement .
        ?statement ?prop_statement ?o .
        filter(contains(str(?prop),"/prop/P"))
        ?statement ?qualifier_prop ?qualifier_obj .
        filter(contains(str(?qualifier_prop),"/prop/qualifier/"))
        OPTIONAL {
            ?qualifier_prop rdfs:label ?qualifier_prop_label .
            values ?langs  {"languages_placeholder" }
            filter(lang(?qualifier_prop_label) = ?langs)
        }
        OPTIONAL {
            ?qualifier_obj rdfs:label ?qualifier_obj_label .
            values ?langs  {"languages_placeholder" }
            filter(lang(?qualifier_obj_label) = ?langs)
        }
    }
}